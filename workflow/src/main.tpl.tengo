// Import required modules
wf := import("@platforma-sdk/workflow-tengo:workflow")
render := import("@platforma-sdk/workflow-tengo:render")
smart := import("@platforma-sdk/workflow-tengo:smart")
file := import("@platforma-sdk/workflow-tengo:file")
exec := import("@platforma-sdk/workflow-tengo:exec")    // Command execution utilities
assets:= import("@platforma-sdk/workflow-tengo:assets") // Asset management
xsv := import("@platforma-sdk/workflow-tengo:pframes.xsv") // CSV/TSV handling
ll := import("@platforma-sdk/workflow-tengo:ll")        // Logging utilities
pSpec := import("@platforma-sdk/workflow-tengo:pframes.spec") // Specification utilities
pFrames := import("@platforma-sdk/workflow-tengo:pframes")
text := import("text")                                   // Text manipulation utilities
json := import("json") // Import json module for encoding

calculateLiabilitiesTpl := assets.importTemplate(":liabilities-calculation")
processTpl := assets.importTemplate(":process")
// Prepare phase of the workflow - collects and sets up required data
wf.prepare(func(args) {

	bundleBuilder := wf.createPBundleBuilder()
	bundleBuilder.ignoreMissingDomains()  // Allow missing domains in the input data
	bundleBuilder.addAnchor("main", args.inputAnchor)  // Set the main anchor point for data

	// Collect all amino acid sequences from the input data
	bundleBuilder.addMulti({
		axes: [{ anchor: "main", idx: 1 }],
	//	name: "pl7.app/vdj/sequence",
		annotations: {
			"pl7.app/vdj/isAssemblingFeature": "true"
		},
		domain: {
			"pl7.app/alphabet": "aminoacid"
			}
		}, "sequences")

	bundleBuilder.addMulti({
		axes: [{ anchor: "main", idx: 1 }],
		name: "pl7.app/vdj/sequence/annotation",
		annotations: {
			"pl7.app/sequence/isAnnotation": "true"
		},
		domain: {
			"pl7.app/alphabet": "aminoacid",
          	"pl7.app/sequence/annotation/type": "CDRs"
		}
	}, "annotations")

	bundleBuilder.addMulti({
		axes: [{ anchor: "main", idx: 1 }],
		name: "pl7.app/vdj/sequence",
		domain: {
			"pl7.app/alphabet": "aminoacid"
		}
	}, "additionalSequences")

		return {
			columns: bundleBuilder.build()  // Build the column bundle for processing
		}
	}
)

wf.body(func(args) {

	blockId := wf.blockId().getDataAsJson()

	columnsWithSequences := args.columns
	sequences := columnsWithSequences.getColumns("sequences")
	annotations := columnsWithSequences.getColumns("annotations")
	additionalSequences := columnsWithSequences.getColumns("additionalSequences")
	datasetSpec := columnsWithSequences.getSpec(args.inputAnchor)
	liabilityTypes := args.liabilityTypes


	// Determine if we're working with single cell data
	isSingleCell := datasetSpec.axesSpec[1].name == "pl7.app/vdj/scClonotypeKey"

	// Start building the table with sequence data
	seqTableBuilder := pFrames.tsvFileBuilder() // Use a different name to avoid confusion before build()
	seqTableBuilder.setAxisHeader(datasetSpec.axesSpec[1], "clonotypeKey")

	// Determine the primary assembling feature (e.g., CDR3)
	// This assumes all 'sequences' share the same core feature or takes the first one's.
	feature := ""
	if len(sequences) > 0 {
		feature = sequences[0].spec.domain["pl7.app/vdj/feature"]
	} else {
		ll.panic("No sequences found to determine the primary feature.") // Or handle as an error/empty case
	}

	hasAnnotations := len(annotations) > 0

	numberingSchema := ""
	for seq_entry in sequences {
		if seq_entry.spec.annotations && seq_entry.spec.annotations["pl7.app/vdj/numberingSchema"] {
			currentSchema := seq_entry.spec.annotations["pl7.app/vdj/numberingSchema"]
			if numberingSchema == "" {
				numberingSchema = currentSchema
			} else if numberingSchema != currentSchema {
				ll.panic("Multiple different numbering schemas found in input sequences. This is not supported. Found: '" + numberingSchema + "' and '" + currentSchema + "'.")
			}
		}
	}

	// Track which features are actually present in the data
	coveredFeatures := {}

	if isSingleCell {
		// Single-cell data handling
		// Add primary chain sequences
		for seq_entry in sequences {
			if seq_entry.spec.name == "pl7.app/vdj/scFv-sequence" {
				for additionalSeq in additionalSequences {
					if additionalSeq.spec.domain["pl7.app/vdj/scClonotypeChain/index"] == "primary" {
						for fr in ["CDR1", "CDR2", "CDR3", "FR1"] {
							if additionalSeq.spec.domain["pl7.app/vdj/feature"] == fr {
								chain := additionalSeq.spec.domain["pl7.app/vdj/scClonotypeChain"]
								chainLabel := (chain == "A" ? "Heavy" : "Light")
								seqTableBuilder.add(columnsWithSequences.getColumn(additionalSeq.key), {header: chainLabel + " " + fr + " aa"})
								if coveredFeatures[fr] {
									coveredFeatures[fr] += 1
								} else {
									coveredFeatures[fr] = 1
								}
							}
						}
					}
				}
			} else if seq_entry.spec.domain["pl7.app/vdj/scClonotypeChain/index"] == "primary" {
				chain := seq_entry.spec.domain["pl7.app/vdj/scClonotypeChain"]
				chainLabel := (chain == "A" ? "Heavy" : "Light")
				seqTableBuilder.add(columnsWithSequences.getColumn(seq_entry.key), {header: chainLabel + " " + " sequence aa"})
			}
		}
		// Add primary chain annotations if they exist
		if hasAnnotations {
			for ann_entry in annotations {
				if ann_entry.spec.domain["pl7.app/vdj/scClonotypeChain/index"] == "primary" {
					chain := ann_entry.spec.domain["pl7.app/vdj/scClonotypeChain"]
					chainLabel := (chain == "A" ? "Heavy" : "Light")
					// Python script expects annotation columns to end with "annotations"
					// and might derive prefix like "Heavy CDRs" or "Light CDRs"
					seqTableBuilder.add(columnsWithSequences.getColumn(ann_entry.key), {header: chainLabel + " annotations"})
				}
			}
		}
	} else { // Bulk data handling
		// Add the primary sequence
		if len(sequences) > 0 {
			if sequences[0].spec.domain["pl7.app/vdj/feature"] == "CDR3" {
				seqTableBuilder.add(columnsWithSequences.getColumn(sequences[0].key), {header: "CDR3 aa"})
			} else {
				seqTableBuilder.add(columnsWithSequences.getColumn(sequences[0].key), {header: "sequence aa"})
			}
		}
		// Add FR/CDR sequences from additionalSequences (bulk, no annotations)
		for additionalSeq in additionalSequences {
			for fr in ["CDR1", "CDR2", "CDR3", "FR1", "FR2", "FR3"] {
				if additionalSeq.spec.domain["pl7.app/vdj/feature"] == fr {
					seqTableBuilder.add(columnsWithSequences.getColumn(additionalSeq.key), {header: fr + " aa"})
					if coveredFeatures[fr] {
						coveredFeatures[fr] += 1
					} else {
						coveredFeatures[fr] = 1
					}
				}
			}
		}
		// Add annotations if they exist
		if hasAnnotations {
			// Python script expects annotation column to end with "annotations"
			seqTableBuilder.add(columnsWithSequences.getColumn(annotations[0].key), {header: "Annotations"})
		}
	}

	features := []
	for k, v in coveredFeatures {
		features += [k]
	}

	// Build the final TSV table
	seqTableBuilder.mem("16GiB")
	seqTableBuilder.cpu(1)
	seqTable := seqTableBuilder.build() // Assign to a new variable

	liabilitiesResultCalc := render.create(calculateLiabilitiesTpl, {
		table: seqTable,
		mapping: hasAnnotations ? annotations[0].spec.annotations["pl7.app/sequence/annotation/mapping"] : undefined,
		liabilityTypes: liabilityTypes,
		numberingSchema: numberingSchema
	})

	liabilitiesResult := liabilitiesResultCalc.output("result", 24 * 60 * 60 * 1000)
	newMapping := liabilitiesResultCalc.output("newMapping", 24 * 60 * 60 * 1000)
	regionsFound := liabilitiesResultCalc.output("regionsFound", 24 * 60 * 60 * 1000)

	processResult := render.create(processTpl, {
		liabilitiesTable: liabilitiesResult,
		newMapping: newMapping,
		regionsFound: regionsFound,
		blockId: blockId,
		customBlockLabel: args.customBlockLabel,
		defaultBlockLabel: args.defaultBlockLabel,
		params: smart.createJsonResource({
			datasetSpec: datasetSpec,
			isSingleCell: isSingleCell,
			coveredFeatures: features,
			hasAnnotations: hasAnnotations,
			liabilityTypes: liabilityTypes
		})
	})

	outputLiabilities := processResult.output("outputLiabilities", 24 * 60 * 60 * 1000)
	exportPframe := processResult.output("exportPframe", 24 * 60 * 60 * 1000)

	// Return the output and export the results
	return {
		outputs: {
			outputLiabilities: outputLiabilities
		},
		exports: {
			exportPframe: exportPframe
		}
	}
})
